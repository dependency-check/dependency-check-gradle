buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath group: 'commons-fileupload', name: 'commons-fileupload', version: '1.3.1'
    }
}

plugins {
    id 'org.owasp.dependencycheck'
    id 'java'
}

sourceCompatibility = 1.5
version = '1.0'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testImplementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-xml', version: '2.7.0'
}

dependencyCheck {
    format = 'csv'
    analyzers.ossIndex.enabled = false
    nvd.datafeedUrl = 'https://dependency-check.github.io/DependencyCheck/hb_nvd/'
}

def dependencyCheckAnalyzeTest = tasks.register('dependencyCheckAnalyzeTest', org.owasp.dependencycheck.gradle.tasks.Analyze) {
    description = 'Runs a dependency-check analysis on just the test configuration.'
    outputDir = project.layout.buildDirectory.dir("reports/dependency-check/test")
    scanConfigurations = ['testRuntimeClasspath']
    skipTestGroups = false
}

def dependencyCheckAnalyzeBuild = tasks.register('dependencyCheckAnalyzeBuild', org.owasp.dependencycheck.gradle.tasks.Analyze) {
    description = 'Runs a dependency-check analysis on just the test configuration.'
    outputDir = project.layout.buildDirectory.dir("reports/dependency-check/build")
    scanConfigurations = []
    scanBuildEnv = true
    scanDependencies = false
}

tasks.dependencyCheckAnalyze {
    description = 'Runs a dependency-check analysis on just the runtime configuration.'
    outputDir = project.layout.buildDirectory.dir("reports/dependency-check/main")
    scanConfigurations = ['runtimeClasspath']

    dependsOn dependencyCheckAnalyzeTest, dependencyCheckAnalyzeBuild
}