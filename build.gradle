/*
 * This file is part of dependency-check-gradle.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Copyright (c) 2015 Wei Ma. All Rights Reserved.
 */

group = 'org.owasp'
version = '1.4.5.2-SNAPSHOT'

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.9.3"
    }
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'com.gradle.plugin-publish'

tasks.withType(GroovyCompile) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
}

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven {
        url "https://repository-dependency-check.forge.cloudbees.com/snapshot/"
    }
}

dependencies {
    compile(
        localGroovy(),
        gradleApi(),
        'commons-collections:commons-collections:3.2.2',
        'org.owasp:dependency-check-core:1.4.5',
        'org.owasp:dependency-check-utils:1.4.5'
    )

    testCompile ('com.netflix.nebula:nebula-test:6.0.1'){
        exclude group: 'org.codehaus.groovy'
    }
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test) {
    group = 'verification'
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
    reports.html.destination = file("$buildDir/reports/integ")
    jvmArgs '-XX:MaxPermSize=256m'
    jvmArgs '-Xmx1024m'
}
check.dependsOn integTest

apply plugin: 'project-report'

apply from: 'conf/publish/local.gradle'
//// gradle uploadArchives
//apply from: 'conf/publish/maven.gradle'
//// gradle publishPlugins
//apply from: 'conf/publish/gradlePluginsPortal.gradle'
//apply from: 'conf/publish/bintray.gradle'        // according to the documentation of plugindev, this line has to be placed and the very end of the build file


defaultTasks 'build'

task wrapper(type: Wrapper) {
  gradleVersion = '3.5'
}
