/*
 * This file is part of dependency-check-gradle.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 * Copyright (c) 2015 Wei Ma. All Rights Reserved.
 */

ext {
    odcVersion = '10.0.1'
    openVulnClientVersion = '5.1.1'
    slackWebhookVersion = '1.4.0'
    spockCoreVersion = '2.3-groovy-3.0'
}

group = 'org.owasp'
version = "${odcVersion}"

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath "com.gradle.publish:plugin-publish-plugin:0.11.0"
    }
}

apply plugin: 'groovy'
apply plugin: 'java-gradle-plugin'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'com.gradle.plugin-publish'

repositories {
    mavenLocal()
    maven {
        name 'OWASP dependency-check snapshot'
        url 'https://oss.sonatype.org/content/repositories/snapshots'
    }
    mavenCentral()
}

dependencies {
    implementation(
        localGroovy(),
        gradleApi()
    )
    api(
        "org.owasp:dependency-check-core:$odcVersion",
        "org.owasp:dependency-check-utils:$odcVersion",
        "io.github.jeremylong:open-vulnerability-clients:$openVulnClientVersion",
        "net.gpedro.integrations.slack:slack-webhook:$slackWebhookVersion"
    )

    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:$spockCoreVersion") {
        exclude module: 'groovy-all'
    }
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.3'
}
test {
    useJUnitPlatform()
}
test.onlyIf { !project.hasProperty('skipTests') }

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId "$group"
            artifactId 'dependency-check-gradle'
            version "$version"
            from components.java
            pom {
                name = 'dependency-check-gradle'
                description = 'OWASP dependency-check gradle plugin is a software ' +
                        'composition analysis tool used to find known vulnerable dependencies.'

                url = 'https://jeremylong.github.io/DependencyCheck/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/dependency-check/dependency-check-gradle/blob/main/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jlong'
                        name = 'Jeremy Long'
                        email = 'jeremy.long@owasp.org'
                    }
                }
                scm {
                    url = 'https://github.com/dependency-check/dependency-check-gradle'
                    connection = 'scm:git:https://github.com/dependency-check/dependency-check-gradle.git'
                    developerConnection = 'scm:git:https://github.com/dependency-check/dependency-check-gradle.git'
                }
            }
        }
        pluginPublication (MavenPublication) {
            groupId "$group"
            artifactId 'dependency-check-gradle'
            version "$version"
            from components.java
            pom {
                name = 'dependency-check-gradle'
                description = 'OWASP dependency-check gradle plugin is a software ' +
                        'composition analysis tool used to find known vulnerable dependencies.'

                url = 'https://jeremylong.github.io/DependencyCheck/'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/dependency-check/dependency-check-gradle/blob/main/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'jlong'
                        name = 'Jeremy Long'
                        email = 'jeremy.long@owasp.org'
                    }
                }
                scm {
                    url = 'https://github.com/dependency-check/dependency-check-gradle'
                    connection = 'scm:git:https://github.com/dependency-check/dependency-check-gradle.git'
                    developerConnection = 'scm:git:https://github.com/dependency-check/dependency-check-gradle.git'
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username project.findProperty('sonatypeUsername') ?: System.getenv("SONATYPE_USER")
                password project.findProperty('sonatypePassword') ?: System.getenv("SONATYPE_PASSWORD")
            }
        }
    }
}

signing {
    sign publishing.publications.pluginPublication
}

pluginBundle {
    website = 'http://jeremylong.github.io/DependencyCheck/dependency-check-gradle/index.html'
    vcsUrl = 'https://github.com/dependency-check/dependency-check-gradle/'
    description = 'A software composition analysis plugin that identifies known vulnerable dependencies used by the project.'
    tags = ['OWASP', 'dependency-check', 'gradle-plugin', 'software-composition-analysis', 'vulnerability-detection', 'security']

    plugins {
        dependencyCheck {
            id = 'org.owasp.dependencycheck'
            displayName = 'OWASP dependency-check-gradle plugin'
        }
    }
    mavenCoordinates {
        groupId = "org.owasp"
    }
}

publish.dependsOn publishPlugins

defaultTasks 'build'
